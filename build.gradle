plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id 'jacoco'
    id 'jacoco-report-aggregation'
    id 'enterprises.iwakura.amber' version '1.1.1'
}

group = 'enterprises.iwakura'
version = '1.2.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":akasha-api-kirara")

    // Personal libraries
    implementation 'enterprises.iwakura:amber-core:1.0.4'
    amber 'enterprises.iwakura:ganyu:1.1.1'
    amber 'enterprises.iwakura:sigewine-core:2.4.0'
    amber 'enterprises.iwakura:jean-core:1.1.1'
    amber 'enterprises.iwakura:jean-gson:1.1.1'

    // Javalin
    amber 'io.javalin:javalin:6.6.0'

    // SSHJ
    amber 'com.hierynomus:sshj:0.40.0'

    // GSON
    amber 'com.google.code.gson:gson:2.13.1'

    // Prometheus
    amber "io.prometheus:prometheus-metrics-core:1.4.3"
    amber 'io.prometheus:prometheus-metrics-instrumentation-jvm:1.4.3'
    amber 'io.prometheus:prometheus-metrics-exporter-servlet-jakarta:1.4.3'

    // Guava
    amber 'com.google.guava:guava:33.4.8-jre'

    // SLF4J
    amber 'org.apache.logging.log4j:log4j-slf4j2-impl:2.23.1'
    amber 'org.apache.logging.log4j:log4j-api:2.23.1'
    amber 'org.apache.logging.log4j:log4j-core:2.23.1'

    // For @Component annotation
    compileOnly 'org.springframework:spring-context:7.0.2'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.+'
    annotationProcessor 'org.projectlombok:lombok:1.18.+'
    testCompileOnly 'org.projectlombok:lombok:1.18.+'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.+'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

sourceSets {
    main {
        compileClasspath += configurations.amber
        runtimeClasspath += configurations.amber
    }
    test {
        compileClasspath += configurations.amber
        runtimeClasspath += configurations.amber
    }
}

shadowJar {
    archiveBaseName.set('akasha')

    manifest {
        attributes 'Main-Class': 'enterprises.iwakura.akasha.AmberBootstrap'
    }

    archiveVersion.set(version)
    archiveClassifier.set('all')
    destinationDirectory = file("$buildDir/libs")
}

// UTF-8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile) {
    options.release = 21
}


def generatedSrcDir = "${buildDir}/generated-src/main/java"

task generateVersionClass {
    doLast {
        def versionClassFile = file("${generatedSrcDir}/enterprises/iwakura/akasha/AkashaVersion.java")
        versionClassFile.parentFile.mkdirs()
        versionClassFile.text =
                """
package enterprises.iwakura.akasha;

/**
 * Auto-generated version class.
 */
public final class AkashaVersion {

    /**
     * Project's current version.
     */
    public static final String VERSION = "${project.version}";

    private AkashaVersion() {
        // Prevent instantiation
    }
}
"""
    }
}

// Include generated sources in compilation
sourceSets {
    main {
        java {
            srcDir generatedSrcDir
        }
    }
}

// Make sure version class is generated before compilation
compileJava.dependsOn generateVersionClass